# upkeeps
@research = 3 # of each
@unity = 4

giga_catalyst_worker = {
    category = specialist
    condition_string = SPECIALIST_JOB_TRIGGER
    building_icon = building_giga_catalyst_workshop
    clothes_texture_index = 3

    possible_pre_triggers = {
        has_owner = yes
        is_being_purged = no
        is_being_assimilated = no
        is_sapient = yes
    }

    possible_precalc = can_fill_specialist_job
    possible = {
        complex_specialist_job_check_trigger = yes
    }

    resources = {
        category = giga_catalyst_category
        upkeep = {
            physics_research = @research
            society_research = @research
            engineering_research = @research
            unity = @unity
        }
    }

    country_modifier = {
        giga_catalyst_crafter_efficiency = 0.01
    }

    triggered_planet_modifier = {
        potential = {
            exists = planet
            planet = {
                is_giga_catalyst_planet = yes
            }
        }
        planet_decision_enact_speed_mult = 0.02
    }

    weight = {
        weight = value:giga_specialist_no_resource_weight|JOB|giga_catalyst_worker|
        mult = 3 # "important" lol
    }
}

giga_catalyst_worker_drone = {
    category = complex_drone
    condition_string = DRONE_JOB_TRIGGER
    building_icon = building_giga_catalyst_workshop
    icon = giga_catalyst_worker

    possible_pre_triggers = {
        has_owner = yes
        is_enslaved = no
        is_being_purged = no
        is_being_assimilated = no
        is_sapient = yes
    }

    possible_precalc = can_fill_drone_job

    possible = { can_think = yes }

    resources = {
        category = giga_catalyst_category
        upkeep = {
            physics_research = @research
            society_research = @research
            engineering_research = @research
            unity = @unity
        }
    }

    country_modifier = {
        giga_catalyst_crafter_efficiency = 0.01
    }

    triggered_planet_modifier = {
        potential = {
            exists = planet
            planet = {
                is_giga_catalyst_planet = yes
            }
        }
        planet_decision_enact_speed_mult = 0.02
    }

    weight = {
        weight = value:giga_complex_drone_no_resource_weight|JOB|giga_catalyst_worker_drone|
        mult = 3 # "important" lol
    }
}
